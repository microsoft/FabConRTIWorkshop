.execute database script <|
.alter table RawShippingMsgs folder 'Bronze Layer'
.create-or-alter function processShippingMsgs()
{
RawShippingMsgs
| extend xmltojson = parse_xml(data)
| project Provider = tostring(['xmltojson']['ShippingEvent']['Provider'])
        , OrderNumber = toguid(['xmltojson']['ShippingEvent']['OrderNumber'])
        , EventTime = todatetime(['xmltojson']['ShippingEvent']['EventTime'])
        , Status = tostring(['xmltojson']['ShippingEvent']['Status'])
        , SourceCity = tostring(['xmltojson']['ShippingEvent']['SourceDistributionCenter']['City'])
        , SourceCountry = tostring(['xmltojson']['ShippingEvent']['SourceDistributionCenter']['Country'])
        , SourceLatitude = toreal(['xmltojson']['ShippingEvent']['SourceDistributionCenter']['Latitude'])
        , SourceLongitude = toreal(['xmltojson']['ShippingEvent']['SourceDistributionCenter']['Longitude'])
        , DestinationCity = tostring(['xmltojson']['ShippingEvent']['DestinationAddress']['City'])
        , DestinationCountry = tostring(['xmltojson']['ShippingEvent']['DestinationAddress']['Country'])
        , DestinationLatitude = toreal(['xmltojson']['ShippingEvent']['DestinationAddress']['Latitude'])
        , DestinationLongitude = toreal(['xmltojson']['ShippingEvent']['DestinationAddress']['Longitude'])
        , WeightKG = toreal(['xmltojson']['ShippingEvent']['WeightKg'])
        , FailureNote = tostring(['xmltojson']['ShippingEvent']['FailureNote'])
        , DowntimeHours = toreal(['xmltojson']['ShippingEvent']['Metadata']['DowntimeHours'])
        , ShippingContents = ['xmltojson']['ShippingEvent']['ShippingContents']
}
.create table ShippingEvents (Provider:string, OrderNumber:guid, EventTime:datetime, Status:string, SourceCity:string, SourceCountry:string, SourceLatitude:real, SourceLongitude:real, DestinationCity:string, DestinationCountry:string, DestinationLatitude:real, DestinationLongitude:real, WeightKG:real, FailureNote:string, DowntimeHours:real, ShippingContents:dynamic) with (folder = "Silver Layer")
.alter table ShippingEvents policy update
```[{
    "IsEnabled": true,
    "Source": "RawShippingMsgs",
    "Query": "processShippingMsgs",
    "IsTransactional": false,
    "PropagateIngestionProperties": true
}]```